/*

Wrapper for HANDLE_CHARSETS with UFT8 as default. Code taken from charset_wrapper by DavidW.

Features:
 - UFT8 as default
 - out_path changed to weidu_external/%MOD_FOLDER%/lang
 - support 'lang', 'languages', or 'tra' out-of-the-box
 - no dependencies and ability to run without AUTO_EVAL_STRINGS
 - copies files over to weidu_external/%MOD_FOLDER%/lang, whether or not conversion is required
 - makes sure all tra are present by using default-language ones if preferred-language ones aren't available

Usage:
 - default_language: the language the mod is written in, if it's not English
 - tra_path: the location of your tra files if it's not 'lang', 'languages', or 'tra'
 - setup_tra: the tra file you keep component names in, if it's not setup.tra
 - load: a space-separated list of tra files you want always to load (the .tra is optional)

Returns:
 - out_path: is where the language files are copied to, changed to weidu_external/%MOD_FOLDER%/lang.

*/

DEFINE_ACTION_FUNCTION handle_charsets_utf8
    STR_VAR
        default_language="english" // set to whatever language you wrote the mod in
        setup_tra="setup" // set to whatever you're keeping your WEIDU installation strings in
        load="" // set to a space-separated list of any tra files you want loaded
        tra_path="" // if it's not 'lang', 'languages', or 'tra'
    RET 
        out_path
    BEGIN
        OUTER_SPRINT out_path "weidu_external/%MOD_FOLDER%/lang"
        // find the tra location
        ACTION_IF "%tra_path%" STR_EQ "" BEGIN
            // try to guess 'lang', 'languages', or 'tra'
            ACTION_IF DIRECTORY_EXISTS "%MOD_FOLDER%/lang" BEGIN
                OUTER_SPRINT tra_path "%MOD_FOLDER%/lang"
            END ELSE
            ACTION_IF DIRECTORY_EXISTS "%MOD_FOLDER%/languages" BEGIN
                OUTER_SPRINT tra_path "%MOD_FOLDER%/languages"
            END ELSE
            ACTION_IF DIRECTORY_EXISTS "%MOD_FOLDER%/tra" BEGIN
                OUTER_SPRINT tra_path "%MOD_FOLDER%/tra"
            END ELSE BEGIN
                FAIL "handle_charsets_utf8 error: you didn't specify tra_path and it's not 'lang', 'languages', or 'tra'."
            END
        END ELSE
        // add '%MOD_FOLDER%' if it's not there already
        ACTION_IF !(INDEX ("%MOD_FOLDER%" "%tra_path%")=0) BEGIN
            OUTER_SPRINT tra_path "%MOD_FOLDER%/%tra_path%"
        END
        // set the no-copy array
        ACTION_CLEAR_ARRAY noconvert_array
        OUTER_SPRINT $noconvert_array(0) "%setup_tra%"
        // set the reload array
        ACTION_CLEAR_ARRAY reload_array
        OUTER_PATCH "%load% " BEGIN
            ind=0
            REPLACE_EVALUATE "\([^ ]+\) " BEGIN
                SPRINT $reload_array("%ind%") "%MATCH1%"
                ++ind
            END
            ""
        END
        // are we on enhanced edition?
        // later versions of EE have bgee.lua. Very early versions of BGEE have monkfist.2da
        OUTER_SET enhanced_edition=( FILE_EXISTS_IN_GAME bgee.lua || FILE_EXISTS_IN_GAME monkfist.2da )
        // are we converting?
        OUTER_SET convert=!(enhanced_edition)
        ACTION_IF convert AND !FILE_EXISTS "%tra_path%/iconv/iconv.exe" BEGIN
            OUTER_SET convert=0
            WARN "handle_charsets_utf8 warning: can't find iconv.exe"
        END
        ACTION_IF convert BEGIN
            OUTER_SPRINT iconv_path "%tra_path%/iconv"
            LAF HANDLE_CHARSETS
                INT_VAR from_utf8=1
                        infer_charsets=1
                STR_VAR tra_path = EVAL "%tra_path%"
                        out_path = EVAL "%out_path%"
                        noconvert_array = noconvert_array
                        reload_array = reload_array
                        default_language = EVAL "%default_language%"
            END

            // run it again for the default language
            ACTION_IF !("%default_language%" STR_EQ "%LANGUAGE%") BEGIN
                LAF HANDLE_CHARSETS
                    INT_VAR from_utf8=1
                            infer_charsets=1
                    STR_VAR tra_path = EVAL "%tra_path%"
                            out_path = EVAL "%out_path%"
                            noconvert_array = noconvert_array
                            reload_array = reload_array
                            language = EVAL "%default_language%"
                END
            END
        END ELSE BEGIN
            // do our own copies
            MKDIR "%out_path%/%LANGUAGE%"
            MKDIR "%out_path%/%default_language%"
            ACTION_BASH_FOR "%tra_path%/%default_language%" ".*\.tra$" BEGIN
                ACTION_IF !"%BASH_FOR_RES%" STR_EQ "%setup_tra%" BEGIN // don't bother copying this
                    COPY "%tra_path%/%default_language%/%BASH_FOR_FILE%" "%out_path%/%default_language%"
                    ACTION_IF !("%default_language%" STR_EQ "%LANGUAGE%") BEGIN
                        ACTION_IF FILE_EXISTS "%tra_path%/%LANGUAGE%/%BASH_FOR_FILE%" BEGIN
                            OUTER_SPRINT tra "%tra_path%/%LANGUAGE%/%BASH_FOR_FILE%"
                        END ELSE BEGIN
                            OUTER_SPRINT tra "%tra_path%/%default_language%/%BASH_FOR_FILE%"
                        END
                        COPY "%tra%" "%out_path%/%LANGUAGE%"
                    END
                END
            END
            // do our own reloads
            ACTION_PHP_EACH reload_array AS int=>tra BEGIN
                ACTION_IF "%tra%" STRING_MATCHES_REGEXP ".+\.tra$" = 0 BEGIN
                    LOAD_TRA "%out_path%/%LANGUAGE%/%tra%"
                END ELSE BEGIN
                    LOAD_TRA "%out_path%/%LANGUAGE%/%tra%.tra"
                END
            END
        END
    END
